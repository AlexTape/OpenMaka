apply plugin: 'com.android.application'

android {

    signingConfigs {
    }

    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "de.alextape.openmaka"
        minSdkVersion 16
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            jni.srcDirs = [] /*disable automatic ndk-build call */
            jniLibs.srcDir 'src/main/jniLibs'
        }
    }

    task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = plugins.getPlugin('com.android.application').sdkHandler.getNdkFolder()
        commandLine "$ndkDir/ndk-build",
                'NDK_PROJECT_PATH=build/intermediates/ndk',
                'NDK_LIBS_OUT=src/main/jniLibs',
                'APP_BUILD_SCRIPT=src/main/jni/Android.mk',
                'NDK_APPLICATION_MK=src/main/jni/Application.mk',
                'V=1' /* debug statement */
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    productFlavors {
        local {
            applicationId = 'de.alextape.openmaka'
        }
        tf {
            applicationId = 'de.alextape.openmaka.tf'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

repositories {
    mavenCentral()
}

configurations {
    compile.exclude module: 'stax'
    compile.exclude module: 'stax-api'
    compile.exclude module: 'xpp3'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.simpleframework:simple-xml:2.7.1'
    compile project(':openCVLibrary2411')
}
